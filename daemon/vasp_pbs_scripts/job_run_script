#!/bin/csh

setenv LGI_DIR /home/${USER}/LGI

setenv CPUS `${LGI_DIR}/daemon/bin/xml -i LGI_job_specifics cpus | grep -E '^([0-9]{1,2})$'`
if ( ${CPUS} <= 0 || ${CPUS} > 12 ) then
   setenv CPUS 2
endif

setenv WALLTIME `${LGI_DIR}/daemon/bin/xml -i LGI_job_specifics walltime | grep -E '^([0-9]{1,2}):([0-9]{1,2}):([0-9]{1,2})$'`
if ( `echo ${WALLTIME} | cut -d':' -f 1` <= 0 || `echo ${WALLTIME} | cut -d ':' -f 1` > 96 ) then
   setenv WALLTIME "96:00:00"
endif

setenv NODES `${LGI_DIR}/daemon/bin/xml -i LGI_job_specifics nodes | grep -E '^([0-9]{1,2})$'`
if ( ${NODES} <= 0 || ${NODES} > 1 ) then
   setenv NODES 1
endif

setenv MEM `${LGI_DIR}/daemon/bin/xml -i LGI_job_specifics mem | grep -E '^([0-9]{1,2})$'`
if ( ${MEM} <= 0 || ${MEM} > 24 ) then
   setenv MEM 4
endif

cat > ./pbs_script << END_OF_PBS_SCRIPT
#PBS -S /bin/csh
#PBS -l nodes=${NODES}:ppn=${CPUS},walltime=${WALLTIME},pmem=${MEM}gb
#PBS -N VASP_pbs
#PBS -o LGI_output

setenv LGI_DIR /home/\${USER}/LGI

setenv REPOSITORY_URL \`\${LGI_DIR}/daemon/bin/xml repository_url < \${PBS_O_WORKDIR}/LGI_job_specifics\`

setenv NODE \`hostname -f\`
cd /scratch/\${USER}/\${PBS_JOBID}

setenv CWD \`pwd\`

if ( \${REPOSITORY_URL} != "" ) then

 setenv REPOSITORY_CONTENT \`\${LGI_DIR}/daemon/bin/LGI_filetransfer -j \${PBS_O_WORKDIR} -x list \${REPOSITORY_URL}\`

 setenv NROFFILES \`echo \${REPOSITORY_CONTENT} | \${LGI_DIR}/daemon/bin/xml number_of_files\`
 setenv FILE_LIST ""

 foreach nr ( \`seq 1 \${NROFFILES}\` )
    setenv FILE_ATTR  number=\\"\${nr}\\"
    setenv FILE_DATA \`echo \${REPOSITORY_CONTENT} | \${LGI_DIR}/daemon/bin/xml file \${FILE_ATTR}\`
    setenv FILE_NAME \`echo \${FILE_DATA} | \${LGI_DIR}/daemon/bin/xml file_name\`
    setenv FILE_LIST "\${FILE_LIST} \${FILE_NAME}"
 end

 if ( "\${FILE_LIST}" != "" ) then
    setenv DATE \`date\`
    echo "VASP_PBS started retrieving repository \${REPOSITORY_URL} to \${NODE}:\${CWD} at \${DATE}." 
    \${LGI_DIR}/daemon/bin/LGI_filetransfer -j \${PBS_O_WORKDIR} download \${REPOSITORY_URL} \${FILE_LIST} > /dev/null
 else
    echo "No files found in repository."
 endif

 if ( -f INCAR ) then
  echo "INCAR was uploaded into repository... Ignoring other input..."
 else
  echo "Creating INCAR from input..."
  dos2unix -n \${PBS_O_WORKDIR}/LGI_input INCAR
 endif

 setenv DATE \`date\`
 echo "VASP_PBS started VASP on node \${NODE} at \${DATE}."
 setenv CHECK_SCRIPT \`\${LGI_DIR}/daemon/bin/xml checkscript < \${PBS_O_WORKDIR}/LGI_job_specifics\` 
 if ( "\${CHECK_SCRIPT}" != "" ) then
   echo "Using \${CHECK_SCRIPT} as check script..."
   cp /home/${USER}/LGI/daemon/vasp_pbs_scripts/\${CHECK_SCRIPT}_vasp52 ./checkscript
   mpiexec /home/${USER}/VASP/vasp_5.2.12_mpi > stdout &
   set PID=\$!
   while ( -d /proc/\${PID} )
     if ( -x ./checkscript ) then
       setenv CHECK \`./checkscript\`
       if ( "\${CHECK}" == "stop" ) then
        kill \${PID}
        wait
       endif
     endif
     sleep 120
   end
 else
   mpiexec /home/${USER}/VASP/vasp_5.2.12_mpi > stdout
 endif
 rm -rf WAVECAR TMPCAR CHG CHGCAR

 setenv FILE_LIST \`ls\`
 if ( "\${FILE_LIST}" != "" ) then
    setenv DATE \`date\`
    echo "VASP_PBS started sending back the output from \${NODE}:\${CWD} to repository \${REPOSITORY_URL} at \${DATE}." 
    \${LGI_DIR}/daemon/bin/LGI_filetransfer -j \${PBS_O_WORKDIR} upload \${REPOSITORY_URL} \${FILE_LIST} > /dev/null
 else
    echo "No files locally created."
 endif

else
 echo "ERROR: No repository was specified for job... Stopping..."
endif

setenv DATE \`date\`
echo "VASP_PBS finished at \${DATE}." 
touch \${PBS_O_WORKDIR}/finished
END_OF_PBS_SCRIPT

qsub pbs_script | cut -d "." -f 1 > ./running
